#!/usr/bin/env perl

use strict;
use warnings;
use FindBin qw($Bin);
use lib "$Bin/../local/lib/perl5";
use HTTP::BrowserDetect;
use POSIX qw(strftime);
use Mojo::Util qw(url_unescape);
use List::Util qw(max);

sub stat_hits_visitors {
	my ($key, $entries) = @_;
	my %stats;
	if ($key eq 'date') {
		foreach (0..6) {
			my $time = time - ($_ * 24 * 60 * 60);
			my $date = strftime('%Y-%m-%d %a', localtime($time));
			$stats{$date}{hits} = 0;
		}
	}
	foreach my $e (@$entries) {
		my $val = $e->{$key};
		next unless length $val;
		if ($key eq 'mobile') {
			$val = $val eq 1 ? 'Mobile' : 'Desktop';
		}
		$val = "<${val}>" if $key eq 'referrer';
		$stats{$val}{hits}++;
		$stats{$val}{visitors}{$e->{ip_hash}} = 1;
	}
	return %stats;
}

sub stats_to_table {
	my ($stats, $sort_date) = @_;
	my @table;
	my $max_hits = max(map { $stats->{$_}->{hits} } keys %$stats) || 1;

	foreach my $key (keys %$stats) {
		push @table, {
			key => $key,
			hits => $stats->{$key}->{hits},
			visitors => scalar keys %{ $stats->{$key}->{visitors} },
		}
	}

	if ($sort_date) {
		@table = sort { $b->{key} cmp $a->{key} } @table;
	} else {
		@table = sort { $b->{hits} <=> $a->{hits} } @table;
		@table = @table[0..9] if @table > 10;
	}

	foreach my $row (@table) {
		my $graph_val = $row->{hits} / $max_hits * 10;
		$row->{graph} = '&#x2588;' x $graph_val;
	}
	return @table;
}

my @entries;

while (my $line = <STDIN>) {
	chomp $line;
	next unless $line;
	my ($time, $ip_hash, $host, $path_query, $referrer, $user_agent, $country) =
		split /\t/, $line;
	my $week_ago = time - (7 * 24 * 60 * 60);
	last if $time < $week_ago;

	my $date = strftime('%Y-%m-%d %a', localtime($time));

	my $ua = HTTP::BrowserDetect->new($user_agent);

	push @entries, {
		date		=> $date,
		ip_hash		=> $ip_hash,
		host		=> $host,
		path_query	=> url_unescape($path_query),
		referrer	=> url_unescape($referrer),
		os			=> $ua->os_string,
		browser		=> $ua->browser_string,
		mobile		=> $ua->mobile,
		country		=> $country || 'unknown',
	}
}

my %date_stats = stat_hits_visitors('date', \@entries);
my @date_table = stats_to_table(\%date_stats, 1);

my %path_query_stats = stat_hits_visitors('path_query', \@entries);
my @path_query_table = stats_to_table(\%path_query_stats);

my %referrer_stats = stat_hits_visitors('referrer', \@entries);
my @referrer_table = stats_to_table(\%referrer_stats);

my %os_stats = stat_hits_visitors('os', \@entries);
my @os_table = stats_to_table(\%os_stats);

my %browser_stats = stat_hits_visitors('browser', \@entries);
my @browser_table = stats_to_table(\%browser_stats);

my %mobile_stats = stat_hits_visitors('mobile', \@entries);
my @mobile_table = stats_to_table(\%mobile_stats);

my %country_stats = stat_hits_visitors('country', \@entries);
my @country_table = stats_to_table(\%country_stats);

my $tables = {
	date => {
		name => 'Date',
		data => \@date_table,
	},
	path_query => {
		name => 'Requested URLs',
		data => \@path_query_table,
	},
	referrer => {
		name => 'Referring URLs',
		data => \@referrer_table,
	},
	os => {
		name => 'Operating System',
		data => \@os_table,
	},
	browser => {
		name => 'Browser',
		data => \@browser_table,
	},
	mobile => {
		name => 'Device',
		data => \@mobile_table,
	},
	country => {
		name => 'Country',
		data => \@country_table,
	}
};

print <<"EOF";
---
title: Stats
layout: wide stats row-hover
---
# Stats

<small>Stats are generated every hour. IP addresses are hashed using a
separately stored secret and deleted after 7 days.</small>
EOF

foreach my $table (qw(date path_query referrer os browser mobile country)) {
	my $name = $tables->{$table}->{name};
	print <<"EOF";

| $name | Hits | Visitors | |
|-------|------|----------|-|
EOF

	my $data = $tables->{$table}->{data};
	foreach my $row (@$data) {
		print "| $row->{key} | $row->{hits} | $row->{visitors} | $row->{graph} |\n"
	}
}
