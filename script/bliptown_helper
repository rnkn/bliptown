#!/usr/bin/env perl

use strict;
use warnings;
use Cwd qw(abs_path);
use IO::Socket::UNIX;
use FindBin qw($Bin);
use lib "$Bin/../local/lib/perl5";
use Data::MessagePack::Stream;

die "$0 must be run as root" unless $> == 0;
die "BLIPTOWN_USER_HOME not defined" unless $ENV{BLIPTOWN_USER_HOME};
die "BLIPTOWN_DOMAIN_LIST not defined" unless $ENV{BLIPTOWN_DOMAIN_LIST};

my $sock_path = '/var/run/bliptown_helper.sock';
unlink $sock_path if -e $sock_path;

my $server = IO::Socket::UNIX->new(
	Type => SOCK_STREAM,
	Local => $sock_path,
	Listen => 5,
) or die "Cannot create socket: $sock_path ($!)";

my $gid = getgrnam('_bliptown') || die "Cannot get gid: _bliptown ($!)";
chown(0, $gid, $sock_path) || die "Cannot chown: $sock_path ($!)";
chmod(0660, $sock_path) || die "Cannot chmod: $sock_path ($!)";

print "Socket created: $sock_path\n";

sub update_domain_list {
	my $payload = shift;
	my $all_domains = $payload->{domains};
	my $domain_file = $ENV{BLIPTOWN_DOMAIN_LIST};
	if (open(my $fh, '>', $domain_file)) {
		foreach (@$all_domains) {
			print $fh "tls keypair \"$_\"\n";
		}
		close $fh or die "Cannot close domain file: $domain_file ($!)";
	} else { die "Cannot open domain file: $domain_file ($!)"; }
	system('rcctl', 'reload', 'relayd');
}

sub get_uid_gid {
	my $user = shift;
	my $uid = getpwnam($user) || die "Cannot get uid: $user ($!)";
	my $gid = getgrnam($user) || die "Cannot get gid: $user ($!)";
	return ($uid, $gid);
}

sub safe_file {
	my ($user, $file) = @_;
	die "Invalid username: $user" unless $user =~ /^[a-zA-Z0-9-_]+$/;
	my $sandbox = $ENV{BLIPTOWN_USER_HOME} . '/' . $user;
	my $file_abs = abs_path($file) || die "Cannot get file: $file ($!)";
	die "File outside of sandbox: $file_abs ($!)"
		unless $file_abs =~ /^\Q$sandbox\E/;
	return $file_abs;
}

sub update_file {
	my $payload = shift;
	my $user = $payload->{user};
	my $file = $payload->{file};
	my $content = $payload->{content};
	my $safe_file = safe_file($user, $file);
	if (open(my $fh, '>', $safe_file)) {
		print $fh $content;
		close $fh;
	} else {
		die "Cannot open file: $file ($!)";
	}
	my ($uid, $gid) = get_uid_gid($user);
	chown($uid, $gid, $safe_file) ||
		die "Cannot set ownership: $file ($!)";
	chmod(0644, $safe_file) ||
		die "Cannot set permissions: $file ($!)";
}

sub rename_file {
	my $payload = shift;
	my $user = $payload->{user};
	my $file = $payload->{file};
	my $new_name = $payload->{new_name};
	my $safe_file = safe_file($user, $file);
	die "File not found: $safe_file ($!)" unless -f $safe_file;
	my $safe_new_name = safe_file($user, $new_name);
	rename($safe_file, $safe_new_name) || die "Cannot rename file: $file ($!)";
}

sub delete_file {
	my $payload = shift;
	my $user = $payload->{user};
	my $file = $payload->{file};
	my $safe_file = safe_file($user, $file);
	unlink $safe_file if -f $safe_file ||
		die "Cannot delete file: $file ($!)";
}

my %allowed_commands = (
	update_file => \&update_file,
	rename_file => \&rename_file,
	delete_file => \&delete_file,
	update_domain_list => \&update_domain_list,
);

my $unpacker = Data::MessagePack::Stream->new();

while (my $conn = $server->accept()) {

    while (read($conn, my $buf, 1024)) {
        $unpacker->feed($buf);

		while ($unpacker->next) {
			my $data = $unpacker->data;
			my $command = $data->{command};
			my $payload = $data->{payload};
			my $success = eval {
				my $func = $allowed_commands{$command} ||
					die "Command not permitted: $command ($!)";

				$func->($payload);
				return 1;
			};
			unless ($success) { warn };
		}
	};

	close($conn);
}
