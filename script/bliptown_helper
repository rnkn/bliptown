#!/usr/bin/env perl

use strict;
use warnings;
use Cwd qw(abs_path);
use IO::Socket::UNIX;
use FindBin qw($Bin);
use lib "$Bin/../local/lib/perl5";
use File::Find;
use Data::MessagePack::Stream;

$SIG{CHLD} = 'IGNORE';

die "$0 must be run as root" unless $> == 0;
die "BLIPTOWN_USER_HOME not defined" unless $ENV{BLIPTOWN_USER_HOME};
die "BLIPTOWN_KEYPAIR_FILE not defined "unless $ENV{BLIPTOWN_KEYPAIR_FILE};
die "BLIPTOWN_ACME_FILE not defined" unless $ENV{BLIPTOWN_ACME_FILE};

my $sock_path = '/var/run/bliptown_helper.sock';
unlink $sock_path if -e $sock_path;

my $server = IO::Socket::UNIX->new(
	Type => SOCK_STREAM,
	Local => $sock_path,
	Listen => 5,
) or die "Cannot create socket: $sock_path ($!)";

my $gid = getgrnam('_bliptown') || die "Cannot get gid: _bliptown ($!)";
chown(0, $gid, $sock_path) || die "Cannot chown: $sock_path ($!)";
chmod(0660, $sock_path) || die "Cannot chmod: $sock_path ($!)";

print "Socket created: $sock_path\n";

sub provision_cert {
	my $payload = shift;
	my $domain = $payload->{domain};
	return unless $domain;
	my $pid = fork();
	if ($pid == 0) {
		exec('acme-client', $domain);
	}
	return 1;
}

sub update_domain_list {
	my $payload = shift;
	my $all_domains = $payload->{domains};
	my $keypair_file = $ENV{BLIPTOWN_KEYPAIR_FILE};
	my $acme_file = $ENV{BLIPTOWN_ACME_FILE};

	if (open(my $fh, '>', $keypair_file)) {
		foreach my $domain (@$all_domains) {
			print $fh "tls keypair \"$domain\"\n";
		}
		close $fh or die "Cannot close keypair file: $keypair_file ($!)";
	} else { die "Cannot open keypair file: $keypair_file ($!)"; }

	if (open(my $fh, '>', $acme_file)) {
		foreach my $domain (@$all_domains) {
			print $fh <<"EOF";
domain $domain {
	alternative names { www.$domain }
	domain certificate "/etc/ssl/$domain.crt"
	domain key "/etc/ssl/private/$domain.key"
	sign with "letsencrypt"
}
EOF
		}
		close $fh or die "Cannot close acme-client file: $acme_file ($!)";
	} else { die "Cannot open acme-client file: $acme_file ($!)" }
	
	my $pid = fork();
	if ($pid == 0) {
		exec('rcctl', 'reload', 'relayd');
	}
	return 1;
}

sub get_uid_gid {
	my $username = shift;
	my @pw = getpwnam($username) or die "Cannot find user: $username";
	my $uid = $pw[2];
	my $gid = $pw[3];
	return ($uid, $gid);
}

sub safe_file {
	my ($username, $filename) = @_;
	die "Invalid username: $username" unless $username =~ /^[a-zA-Z0-9-_]+$/;
	my $sandbox = $ENV{BLIPTOWN_USER_HOME} . '/' . $username;
	my $filename_abs = abs_path($filename) || die "Cannot get file: $filename ($!)";
	die "File outside of sandbox: $filename_abs ($!)"
		unless $filename_abs =~ /^\Q$sandbox\/\E/;
	return $filename_abs;
}

sub delete_empty_dirs {
	my $dir = shift;
	find(sub { return unless -d $_; rmdir $_}, $dir);
}

sub write_blob {
	my $payload = shift;
	my $username = $payload->{username};
	my $filename = $payload->{filename};
	my $blob = $payload->{blob};
	my $safe_filename = safe_file($username, $filename);
	open my $fh, '>', $safe_filename || die "Cannot write file: $filename ($!)";
	binmode $fh;
	print $fh $blob;
	close $fh;
	my ($uid, $gid) = get_uid_gid($username);
	chown($uid, $gid, $safe_filename) ||
		die "Cannot set ownership: $filename ($!)";
	chmod(0644, $safe_filename) ||
		die "Cannot set permissions: $filename ($!)";
	return 1;
}

sub update_file {
	my $payload = shift;
	my $username = $payload->{username};
	my $filename = $payload->{filename};
	my $content = $payload->{content};
	my $safe_filename = safe_file($username, $filename);
	if (open(my $fh, '>', $safe_filename)) {
		print $fh $content;
		close $fh;
	} else {
		die "Cannot open file: $filename ($!)";
	}
	my ($uid, $gid) = get_uid_gid($username);
	chown($uid, $gid, $safe_filename) ||
		die "Cannot set ownership: $filename ($!)";
	chmod(0644, $safe_filename) ||
		die "Cannot set permissions: $filename ($!)";
	return 1;
}

sub rename_file {
	my $payload = shift;
	my $username = $payload->{username};
	my $filename = $payload->{file};
	my $new_filename = $payload->{new_filename};
	my $safe_filename = safe_file($username, $filename);
	die "File not found: $safe_filename ($!)" unless -f $safe_filename;
	my $safe_new_filename = safe_file($username, $new_filename);
	rename($safe_filename, $safe_new_filename) || die "Cannot rename file: $filename ($!)";
	return 1;
}

sub delete_file {
	my $payload = shift;
	my $username = $payload->{username};
	my $filename = $payload->{filename};
	my $safe_file = safe_file($username, $filename);
	unlink $safe_file if -f $safe_file ||
		die "Cannot delete file: $filename ($!)";
	return 1;
}

my %allowed_commands = (
	write_blob  => \&write_blob,
	update_file => \&update_file,
	rename_file => \&rename_file,
	delete_file => \&delete_file,
	provision_cert => \&provision_cert,
	update_domain_list => \&update_domain_list,
);

my $unpacker = Data::MessagePack::Stream->new();

while (my $conn = $server->accept()) {

    while (read($conn, my $buf, 1024)) {
        $unpacker->feed($buf);

		while ($unpacker->next) {
			my $data = $unpacker->data;
			my $command = $data->{command};
			my $payload = $data->{payload};
			my $success = eval {
				my $func = $allowed_commands{$command} ||
					die "Command not permitted: $command ($!)";

				$func->($payload);
				return 1;
			};
			unless ($success) { warn };
		}
	};

	close($conn);
}
